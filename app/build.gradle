buildscript {
  ext.versions = [
    kotlin: '1.1.1'
  ]

  repositories {
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE'
  }
}

plugins {
  id 'org.jetbrains.kotlin.jvm' version '1.1.1'
  id 'au.com.dius.pact' version '3.3.7'
  id 'com.wiredforcode.spawn' version '0.8.2'
  id 'org.springframework.boot' version '1.5.2.RELEASE'
}

jar {
  baseName = 'dius-mentor_boris_coffee-api'
  version = '0.1.0'
}

repositories {
  mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

springBoot {
  mainClass = 'au.com.dius.coffee.ApplicationKt'
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib:${versions.kotlin}"
  compile "org.jetbrains.kotlin:kotlin-reflect:${versions.kotlin}"

  compile 'org.springframework.boot:spring-boot-starter-web', {
    exclude module: 'spring-boot-starter-tomcat'
  }
  for (def starter in ['jetty', 'actuator', 'data-jpa', 'jdbc']) {
    compile "org.springframework.boot:spring-boot-starter-${starter}"
  }

  compile 'com.h2database:h2'
  compile 'com.fasterxml.jackson.module:jackson-module-kotlin'

  testCompile 'junit:junit'
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:${versions.kotlin}"
  testCompile 'org.springframework.boot:spring-boot-starter-test'
}

import com.wiredforcode.gradle.spawn.*
task startProvider(type: SpawnProcessTask, dependsOn: 'assemble') {
  command "java -Dspring.profiles.active=test -jar ${jar.archivePath}"
  ready 'Started ApplicationKt'
}
task stopProvider(type: KillProcessTask) { }

pact {
  serviceProviders {
    'Coffee Ordering Provider' {
      port = 8080

      startProviderTask = startProvider
      terminateProviderTask = stopProvider
      stateChangeUrl = url('http://localhost:8080/pactStateChange')

      hasPactsWith('Coffee Ordering Consumers') {
        pactFileLocation = file("$buildDir/pacts")
      }
    }
  }
}
